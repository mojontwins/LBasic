# Configuramos el text box para que aparezca en la parte inferior
tb config 1 18 78 6 0 7 15 0 8

# Esto carga la pantalla a doble pixel de forma fancy
pix "pix/CHALET.PIX" dbl fancy

# Esperamos dos segundos
sleep 2.0

# Y mostramos nuestra primera caja de textos
tbwc "Entusiasmado haces todos los preparativos. Te montas en tu viejo coche del 71 dispuesto a vivir muchas aventuras. Los nervios te juegan una mala pasada y te pierdes un par de veces... ­te haces un l¡o con los mapas! Por suerte, a media ma¤ana, consigues llegar a la direcci¢n que ven¡a en la carta. ­Es impresionante! Se trata de un chal‚ enorme en una urbanizaci¢n de super lujo..."

sleep 1.0

tbwc "­Empieza, pues tu aventura! Recuerda actuar con cabeza, ­no hagas locuras! te bajas el coche y caminas con paso firme a la entrada de la casa . . ." "­Vamos al l¡o!"

fancy_cls

:puerta_start
music midi "mid/TENTACUL.MID" loop

pix "pix/PUERTA.PIX" dbl fancy

# *N significa que la coordenada se multiplicar  por 2
# (y as¡ casar  con los gr ficos a doble tama¤o)
# Obviamente esto s¢lo tiene sentido en juegos as¡ (mis ports de cosas viejas)

zones reset
zones def "Pomo" *191 *65 *207 *83 actions
zones def "Puerta" *104 *0 *210 *149 actions
zones def "Timbre" *219 *15 *237 *32 actions
zones def "C‚sped" *0 *149 *319 *199 actions
zones def "Pared" *0 *0 *104 *148 actions
zones def "Pared" *211 *0 *319 *148 actions

# Ahora dejamos que el usuario se mueva con el rat¢n por la pantalla.
# Cuando haga click, se abrir  la serie de men£s correspondiente y se
# generar  un `go` a la etiqueta correcta (ver el manual)

:puerta_run
zones run :puerta :puerta_run

# Si no se puls¢ ning£n sitio o el sitio que se puls¢ no ten¡a `go`
# correspondiente la ejecuci¢n sigue, sencillamente... As¡ no tienes
# que programar respuestas para todas las combinaciones. Sencillamente
# puedes hacer: 

tbwc "Eso no parece que sirva para nada..." wt

# El comando `ret` genera un `go` a la segunda etiqueta que especificamos 
# en `zones run`, o sea, har¡a un `go :puerta_run`.

ret

# todos los 'Hablar' fallan
# Aqu¡ hay etiquetas asociadas a varios `item run`, pero `ret`
# siempre volver  al sitio correcto.

:puerta_pomo_hablar
:puerta_puerta_hablar
:puerta_timbre_hablar
:puerta_c‚sped_hablar
:puerta_pared_hablar
:salon_chimenea_hablar
:salon_cierro_hablar
:cocina_alacena_hablar
:cocina_alacena__hablar
:cocina_grifo_hablar
:cocina_ventana_hablar

tbwc "­Vaya! ¨Otra vez hablando con tu amigo imaginario? ­Pues empezamos bien! ­Como te vean, van a pensar que no est s en tus cabales!"
ret

# Pomo
:puerta_pomo_mirar
tbwc "Un pomo bien caro, de metal macizo bru¤ido. Debe pesar como que dos kilos. Seguro que este pomo vale m s que todo el porche de tu casa."
ret

:puerta_pomo_manipular
tbwc "Pues no se mueve ni un poquito. Y te parece muy normal."
ret

# Puerta
:puerta_puerta_mirar
tbwc "Una maciza puerta met lica que parece que pudiera soportar un ob£s, y que sirve de entrada a la casa de Mar¡a (esperas que no te hayas equivocado de casa). Est  cerrada a cal y canto. Debe haber cosas muy valiosas al otro lado."
ret

:puerta_puerta_manipular
tbwc "Ni se cantea. La puerta encaja perfectamente en los goznes. Podr¡a servir como exclusa de un submarino."
ret

# Timbre
:puerta_timbre_mirar
tbwc "Un timbre el‚ctrico precioso. De muy alta calidad. Fabricado con pl sticos nobles."
ret

:puerta_timbre_manipular
tbwc "Oyes un melodioso DING DONG que parece grabado por la Filarm¢nica de Londres. Esperas unos instantes y alguien te abre la puerta" cbc wt
go :yoli1_start

# C‚sped
:puerta_c‚sped_mirar
tbwc "Bonito, fresco, bien cuidado. Cada brizna parece ser exactamente de la misma longitud de las dem s. Casi te da pena pisarlo con tus indignas chanclas de todo a 100."
ret

:puerta_pared_mirar
tbwc "La pared est  un poco resquebrajada. No sabes si tendr  que ver con la salinidad del ambiente, ya que la casa est  pr cticamente a pie de playa, o quiz  se sea as¡ por dise¤o. Probablemente sea esto £ltimo. La gente con pasta es bastante extravagante."
ret

# 

:yoli1_start
fancy_cls
pix "pix/YOLI1.PIX" dbl fancy
tb config 50 9 29 6 0 7 255 0 8

tbwc "Eh... ­Hola! ¨Y t£ quien eres?" "Extra¤a"
tbwc "Ah, hola..." "T£"
tbwc "Vaya... ¨Qui‚n es esta? ­Ya lo s‚! ­Me he equivocado de casa!" "T£ (pensando)"
tbwc "Me llamo [...]. Perdona, ¨es esta la casa de Mar¡a?" "T£"
tbwc "S¡, claro... ¨Eres una de sus amistades de la ciudad o algo? No me suena nada tu cara" "Extra¤a"
tbwc "S¡. Nos conocemos del a¤o pasado. Recib¡ una carta la semana pasada en la que me invitaba a pasar unos d¡as aqu¡, en la playa" "T£"
tbwc "Pues ella no est  aqu¡ ahora mismo. Ha salido a hacer unas cosas." "Extra¤a"
tbwc "¨Te importa si paso y la espero dentro? ­Es que hace m£ch¡simo calor!" "T£"
tbwc "Oh, s¡, claro, ­qu‚ tonta! Pasa, pasa." "Extra¤a"

:vestibulo_start
fancy_cls
tb config 1 18 78 6 0 7 15 0 8

tbwc "­Menos mal! Casi ibas a desfallecer por el calor. "

pix "pix/VESTIB.PIX" dbl fancy

zones reset
zones def "Vest¡bulo" *0 *0 *319 *199 actions

:vestibulo_run
zones run :vestibulo :vestibulo_run
tbwc "Eso no parece que sirva para nada..." wt
ret

:vestibulo_vest¡bulo_mirar
tbwc "La estancia a la que has accedido es un vest¡bulo oscuro y t‚trico. Varias puertas parecen llevar a otras zonas de la enorme casa, y hay una escalera que lleva al piso superior. Antes de que puedas fijarte mejor, la chica se acerca desde atr s y te toca en el hombro."

fancy_cls
tb config 50 9 29 6 0 7 255 0 8
pix "pix/YOLI1.PIX" dbl fancy

tbwc "Ay, perdona, no te he dicho mi nombre. Me llamo Yolanda, pero me dicen Yoli. Soy una amiga de la infancia de Mar¡a." "Extra¤a"
tbwc "Yo tambi‚n estoy pasando aqu¡ unos d¡as con ella... ­la casa es tan grande!" "Yoli"
tbwc "Mar¡a me dijo que ten¡a que bajar al pueblo a comprar algunas cosas que le hac¡an falta. No creo que tarde mucho." "Yoli"
tbwc "Mucho gusto en conocerte, Yoli." "T£"
tbwc "­Pero no te quedes ah¡ de pie! Cuando has llamado me has pillado yendo al jard¡n a tomar el sol." "Yoli"
tbwc "¨Por qu‚ no vienes conmigo y charlamos un rato hasta que llegue Maria? ¨Te apetece?" "Yoli"
tbwc "Claro, ser  agradable." "T£"

:cocina_intro
fancy_cls
tb config 1 20 78 4 0 7 15 0 8
pix "pix/COCINA.pix" dbl fancy

tbwc "Entr is por la primera puerta, que da a la cocina. No te da tiempo de deleitarte con los muebles de los a¤os ochenta: os dirig¡s directamente a la puerta que da a un jard¡n con una piscina. Por desgracia, parece que la puerta est  cerrada con llave..."

fancy_cls
tb config 50 9 29 6 0 7 255 0 8
pix "pix/YOLI1.PIX" dbl fancy

tbwc "­Vaya, hombre! ­Ahora est  cerrada!" "Yoli"
tbwc "¨No sabes donde est  la llave?" "T£"
tbwc "Qu‚ va. Creo que he visto a Mar¡a guardarla por el sal¢n o por ah¡, pero no se d¢nde..." "Yoli"
tbwc "­Voy a buscarla!" "T£"

# Flags:
let ventana 0

:cocina_start
fancy_cls
tb config 1 20 78 4 0 7 15 0 8
pix "pix/COCINA.pix" dbl fancy

zones reset
zones def "Alacena" *0 *28 *36 *101 actions
zones def "Alacena" *31 *33 *88 *75 actions
zones def "Alacena" *176 *51 *194 *95 actions
zones def "Alacena" *210 *48 *231 *158 actions
zones def "Alacena" *239 *40 *302 *71 actions
zones def "Alacena" *0 *157 *49 *189 actions
# El jugador no ver  el espacio pero genera otra etiqueta!
zones def "Alacena " *127 *136 *167 *175 actions
zones def "Grifo" *126 *108 *137 *122 actions
zones def "Ventana" *94 *70 *158 *112 actions

zones def "Ir al vest¡bulo" *0 *190 *300 *199 :vestibulo2_start
zones def "Ir a la piscina" *301 *0 *319 *199

:cocina_run
zones run :cocina :cocina_run
tbwc "Eso no parece que sirva para nada..." wt
ret

:cocina_alacena_mirar
:cocina_alacena__mirar
tbwc "Aunque la casa es muy lujosa y los materiales son los m s refinados, esto no deja de ser una alacena hortera de los a¤os ochenta, a juego con el resto de la casa."
ret

:cocina_alacena_manipular
tbwc "Abres la puerta pero s¢lo encuentras todo tipo de cosas normales que ahora mismo no te sirven para nada."
ret

:cocina_alacena__manipular
items has "Aceite" :cocina_alacena__aceite
tbwc "En esta alacena hay un bote de aceite. Decides cogerlo, algo te dice que puede serte util"
items put "Aceite"
ret

:cocina_alacena__aceite
tbwc "Abres la puerta pero s¢lo encuentras todo tipo de cosas normales que ahora mismo no te sirven para nada."
ret

:cocina_alacena_inventario_aceite
:cocina_alacena__inventario_aceite
tbwc "No es necesario. La puerta de la alacena funciona perfectamente."
ret

:cocina_grifo_mirar
tbwc "Este grifo monomando marca 'piedra' era la cosa m s moderna y m s chic que hab¡a en 1984. Ahora parece que gotea un poco."
ret

:cocina_grifo_manipular
tbwc "Ahora no tienes sed. Ni quieres ensuciar un vaso."
ret

:cocina_ventana_mirar
:salon_cierro_mirar
tbwc "­Qu‚ paisaje tan bonito! Prados de verde c‚sped terminan en un acantilado que da a la playa. Las potentes olas del oc‚ano rompen con fuerza sobre la fina y dorada arena. Detr s de todo eso, en un islote, hay una central nuclear sobrevolada por una bandada de p jaros de tres cabezas."
ret

# Esto no sirve para nada pero es un ejemplo tonto de variables

:cocina_ventana_manipular
eq %ventana 1 :cocina_ventana_cierra

let ventana 1
tbwc "Abres la ventana"
ret

:cocina_ventana_cierra
let ventana 0
tbwc "Cierras la ventana"
ret

:cocina_ir_a_la_piscina
items has "Llave" :piscina_has_llave
tbwc "Creo que a lo mejor no estabas prestando atenci¢n cuando Yoli te dijo que hac¡an falta unas llaves para abrir la puerta del patio que lleva a la piscina..."
ret

:piscina_has_llave
items has "Aceite" :piscina_has_aceite
tbwc "Introduces la llave en la cerradura. ­Perfecto, es la llave correcta! la giras un poco pero la manija parece atascada por el ¢xido"
fancy_cls
pix "pix/YOLI1.PIX" dbl fancy
tb config 50 9 29 6 0 7 255 0 8
tbwc "¨Qu‚ pasa? ¨No se abre?" "Yoli"
tbwc "Qu‚ va... Parece que est  oxidada o algo. La manija se queda atascada." "T£"
tbwc "A lo mejor con algo para lubricar... Busca por las alacenas, ­debe haber un poco de aceite por alg£n sitio!" "Yoli"
fancy_cls
tb config 1 20 78 4 0 7 15 0 8
pix "pix/COCINA.PIX" dbl fancy
ret 

:vestibulo2_start
fancy_cls
pix "pix/VESTIB.PIX" dbl fancy

zones reset
zones def "Salida" *0 *84 *27 *190 :salida
zones def "Ir al sal¢n" *57 *98 *105 *180 :salon_start
zones def "Ir a la cocina" *130 *97 *179 *181 :cocina_start
zones def "Escaleras" *202 *37 *290 *182 :escaleras

:vestibulo2_run
zones run :vestibulo2 :vestibulo2_run
tbwc "Ah¡ no hay nada interesante y no est s para perder el tiempo"
ret

:escaleras
tbwc "Mejor no. Mar¡a a£n no sabe que est s aqu¡ y no crees que sea buena idea ponerte a curiosear por toda la casa sin permiso."
ret

:salida
tbwc "­Claro que s¡! ­Seguro que quieres irte ahora! Anda, vuelve y sigue buscando una forma de entrar en el patio..."
ret

:salon_start
fancy_cls
pix "pix/SALON.PIX" dbl fancy

zones reset
zones def "Ir al vest¡bulo" 0 *184 *319 *199 :vestibulo2_start
zones def "L mpara" *24 *69 *54 *76 actions
zones def "Chimenea" *210 *103 *268 *152 actions
zones def "Mueble" *188 *50 *212 *140 actions
zones def "Cierro" *67 *66 *139 *130 actions

:salon_run
zones run :salon :salon_run
tbwc "Parece que eso no sirve de mucho ..."
ret

:salon_cierro_manipular
:salon_cierro_inventario_aceite
:salon_cierro_inventario_llave
tbwc "El cierro no da al patio, as¡ que no te parece que sea interesante ponerte a juguetear con la puerta que conduce a ‚l ni perder m s tiempo haciendo tonter¡as."
ret

:salon_mueble_mirar
tbwc "Se trata de un bonito mueble de maderas nobles, hecho a mano y de dise¤o. No parece mucho m s £til que la bazofia que tienen tus padres en el sal¢n, pero seguro que es mucho m s caro."
ret

:salon_mueble_hablar
tbwc "Es un mueble muy caro, pero no es tan caro como para poder responderte."
ret

:salon_mueble_manipular
items has "Llave" :mueble_con_llave

tbwc "El mueble tiene un compartimento en la parte superior, pero est  firmemente cerrado y no eres capaz de abrirlo. Junto al cierre hay una ranura como para un llavec¡n"
ret

:mueble_con_llave
tbwc "No hay nada m s que te interese o te sirva en el mueble."
ret

:salon_mueble_inventario_llavec¡n
items has "Llave" :mueble_con_llave

tbwc "Pruebas el llavec¡n en la ranura que hay junto al cierre del compartimento y lo abres. ­Dentro est  una llave vieja con todo el aspecto de ser la del patio! Te la guardas."
items put "Llave"
items remove "Llavec¡n"
ret

:salon_l mpara_mirar
:salon_l mpara_manipular
items has "Llave" :lampara_nada
items has "Llavec¡n" :lampara_nada

tbwc "Palpas por la parte superior, dentro del plaf¢n, y notas un peque¤o objeto. ­Es un llavec¡n! Lo guardas por si acaso."
items put "Llavec¡n"
ret

:lampara_nada
tbwc "Ya no hay m s llaves en la l mpara ni nada m s que pueda servirte de algo."
ret

:salon_chimenea_mirar
tbwc "Aunque est  claro que esta casa se utiliza m s que nada para pasar las vacaciones de verano, tiene que ser muy agradable estar aqu¡ en invierno, junto a la chimenea, mientras fuera arrecia el temporal."
ret

:salon_chimenea_manipular
tbwc "Hace mucho calor para encender un fuego ahora... Adem s, Yoli te espera en la cocina para que abras la puerta del patio, por si no lo recuerdas."
ret

:piscina_has_aceite
tbwc "­Ahora s¡! Con las llaves y un poquito de aceite en la cerradura abres la puerta del patio."

items remove "Aceite"
items remove "Llave"
